plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-kapt'
}

String KEY = System.getenv('APPLE_CLASSIFIER_KEY')
String PASSWORD = System.getenv('APPLE_CLASSIFIER_PASSWORD')

android {
    compileSdk 31

    defaultConfig {
        applicationId "pt.carvalho.apples.classifier"
        minSdk 21
        targetSdk 31
        versionCode rootProject.ext.code()
        versionName rootProject.ext.name()

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        vectorDrawables {
            useSupportLibrary true
        }
    }

    buildFeatures {
        compose true
    }

    testOptions {
        reportDir = "$project.buildDir/testResults"
    }

    signingConfigs {
        release {
            storeFile rootProject.file('./secrets/release.jks')
            keyAlias KEY
            keyPassword PASSWORD
            storePassword PASSWORD
        }
    }

    aaptOptions {
        noCompress "tflite"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            applicationIdSuffix '.debug'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    composeOptions {
        kotlinCompilerExtensionVersion rootProject.ext.versions.compose
    }

    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation rootProject.ext.dependencies.activityCompose
    implementation rootProject.ext.dependencies.activityKtx
    implementation rootProject.ext.dependencies.appCompat
    implementation rootProject.ext.dependencies.cameraX
    implementation rootProject.ext.dependencies.cameraXLifecycle
    implementation rootProject.ext.dependencies.cameraXView
    implementation rootProject.ext.dependencies.coil
    implementation rootProject.ext.dependencies.composeAnimation
    implementation rootProject.ext.dependencies.composeInsets
    implementation rootProject.ext.dependencies.composeMaterial
    implementation rootProject.ext.dependencies.composePermissions
    implementation rootProject.ext.dependencies.composeUITooling
    implementation rootProject.ext.dependencies.coreKtx
    implementation rootProject.ext.dependencies.hilt
    implementation rootProject.ext.dependencies.coroutines
    implementation rootProject.ext.dependencies.material
    implementation rootProject.ext.dependencies.tensorflowVision

    kapt rootProject.ext.dependencies.hiltCompiler

    testImplementation rootProject.ext.dependencies.junit
    testImplementation rootProject.ext.dependencies.mockito

    debugImplementation rootProject.ext.dependencies.composeTestManifest
    androidTestImplementation rootProject.ext.dependencies.composeJunit
    androidTestImplementation rootProject.ext.dependencies.composeUITest
}